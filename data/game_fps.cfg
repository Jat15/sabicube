// fps specific cfg stuff goes here

togglezoom = [ zoom (if (= $zoom 1) -1 1) ]
sayteamcommand = [inputcommand (if (>= $numargs 1) [result $arg1] [result ""]) [sayteam $commandbuf] "[team]"]

mapcomplete = [ complete $arg1 packages/base ogz ]
mapcomplete map

modenames = "ffa coop teamplay insta instateam effic efficteam tac tacteam capture regencapture ctf instactf protect instaprotect hold instahold efficctf efficprotect effichold collect instacollect efficcollect"
loop i (listlen $modenames) [
    mname = (at $modenames $i)
    alias $mname [ if (mode @i) [if (> $numargs 0) [map $arg1] [map]] ]
    mapcomplete $mname
]

spmodenames = "sp dmsp"
loop i (listlen $spmodenames) [
    mname = (at $spmodenames $i)
    alias $mname [ if (mode (- @i 3)) [if (> $numargs 0) [map $arg1] [map]] ]
    mapcomplete $mname
]

complete importcube packages/cube cgz

demo = [ stopdemo; if (mode -1) [if (> $numargs 0) [map $arg1] [map]] ]
complete demo . dmo

speditlock = 0
allowspedit = [ speditlock = 0; onrelease [speditlock = 1] ]

allowedittoggle = [
    if (&& (m_sp (getmode)) $speditlock) [
        error "you must hold" (prettylist (searchbinds "allowspedit") "or") "to toggle editing in single player modes"
        result 0
    ] [
        result 1
    ]
]

playermodelnum = 2

playermodeldir0 = "playermodel/male"
playermodelicon0 = "male"
playermodelname0 = "male model"
playermodelstory0 = [This is the default male playermodel]

playermodeldir1 = "playermodel/female"
playermodelicon1 = "female"
playermodelname1 = "female model"
playermodelstory1 = [This is the default female playermodel]
 
playermodeldir = [getalias (concatword "playermodeldir" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelicon = [getalias (concatword "playermodelicon" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelname = [getalias (concatword "playermodelname" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelstory = [getalias (concatword "playermodelstory" (if (> $numargs 0) [result $arg1] [result $playermodel]))]

playasong = [ music (concatword "music/" (+ 1 (rnd 8)) ".ogg") playasong ]
playasong

